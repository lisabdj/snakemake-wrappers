rule create_dict:
    input:
        "genome.fasta",
    output:
        "genome.dict",
    threads: 1
    resources:
        mem_mb=1024,
    log:
        "logs/picard/create_dict.log",
    params:
        extra="",
    wrapper:
        "master/bio/picard/createsequencedictionary"


rule samtools_index:
    input:
        "genome.fasta",
    output:
        "genome.fasta.fai",
    log:
        "logs/genome_index.log",
    params:
        extra="",  # optional params string
    wrapper:
        "master/bio/samtools/faidx"


rule picard_replace_rg:
    input:
        "mapped/{sample}.bam",
    output:
        "mapped/{sample}.group.bam",
    threads: 1
    resources:
        mem_mb=1024,
    log:
        "logs/picard/replace_rg/{sample}.log",
    params:
        # Required for GATK
        extra="--RGLB lib1 --RGPL illumina --RGPU {sample} --RGSM {sample}",
    wrapper:
        "master/bio/picard/addorreplacereadgroups"


rule mutect2:
    input:
        fasta="genome.fasta",
        map="recal/{sample}.bam",
        intervals="regions.bed",
    output:
        vcf="variant/{sample}.vcf",
        bam="variant/{sample}.bam",
        f1r2="counts/{sample}.f1r2.tar.gz",
    threads: 1
    resources:
        mem_mb=1024,
    params:
        extra=" --tumor-sample {sample}_tumor "
    log:
        "logs/mutect/{sample}.log",
    wrapper:
        "master/bio/gatk/mutect"


rule gatk_get_pileup_summaries:
    input:
        bam="recal/{sample}.bam",
        variants="known.vcf.gz",
        intervals="regions.bed",
    output:
        "summaries/{sample}.table",
    threads: 1
    resources:
        mem_mb=1024,
    params:
        extra="",
    log:
        "logs/summary/{sample}.log",
    wrapper:
        "master/bio/gatk/getpileupsummaries"


#TODO: CalculateContamination


rule gatk_learnreadorientationmodel:
    input:
        f1r2="counts/{sample}.f1r2.tar.gz",
    output:
        "artifacts_prior/{sample}.tar.gz",
    threads: 1
    resources:
        mem_mb=1024,
    params:
        extra="",
    log:
        "logs/learnreadorientationbias/{sample}.log",
    wrapper:
        "master/bio/gatk/learnreadorientationmodel"



rule filtermutectcalls_complete:
    input:
        vcf="variant/{sample}.vcf",
        ref="genome.fasta",
        bam="recal/{sample}.bam",
        contamination="summaries/{sample}.table",
        # segmentation="", # from gatk CalculateContamination
        f1r2="artifacts_prior/{sample}.tar.gz",
    output:
        vcf="variant/{sample}.filtered.vcf.gz",
        vcf_idx="variant/{sample}.filtered.vcf.gz.tbi"
    threads: 1
    resources:
        mem_mb=1024,
    log:
        "logs/gatk/filter/{sample}.log",
    params:
        extra="--create-output-variant-index --min-median-mapping-quality 35 --max-alt-allele-count 3",
        java_opts="",
    wrapper:
        "master/bio/gatk/filtermutectcalls"